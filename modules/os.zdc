_c :: import c;
std :: import std;
posix :: import posix;

using std;

_og :: import os_generic;
using _og;

_thread :: import thread;
using _thread;

static_if (PLATFORM_WINDOWS)
{
    os_specific :: import os_windows;
} 
else static_if (PLATFORM_LINUX)
{
    os_specific :: import os_linux;
}

get_cwd :: () -> String
{
    found := false;
    result := get_environment_variable("PWD", *found);
    assert(found);
    return result;
}

get_environment_variable :: (name: String, found: *bool) -> String
{
    c_str := posix.getenv(name.data);

    if (c_str)
    {
        <found = true;
        return string_ref(c_str);
    }

    <found = false;
}

get_full_path :: (name: String) -> String
{
    return os_specific.get_full_path(name);
}

is_directory :: (path: String) -> bool
{
    return os_specific.is_directory(path);
}

is_regular_file :: (path: String) -> bool
{
    return os_specific.is_regular_file(path);    
}

list_files_in_directory :: (path: String) -> Array(String)
{
    return os_specific.list_files_in_directory(path);
}

read_file_string :: (path: String, result_ptr: *bool) -> String
{
    assert(is_regular_file(path));

    file := posix.fopen(path.data, "rb");

    if (file)
    {
        posix.fseek(file, 0, posix.SEEK_END);
        file_size := posix.ftell(file);
        posix.fseek(file, 0, posix.SEEK_SET);

        result_str : String = { null, (:u64)file_size };
        result_str.data = (:*u8)_c.malloc((:u64)file_size + 1);

        read_result := posix.fread(result_str.data, (:u64)file_size, 1, file);
        assert(read_result == 1);

        result_str.data[file_size] = '\0';

        posix.fclose(file);

        <result_ptr = true;
        return result_str;
    }

    <result_ptr = false;
}

// get_terminal_state :: () -> os_specific.Terminal_State
// {
//     return os_specific.get_terminal_state();
// }

// set_terminal_state :: (new_state: os_specific.Terminal_State)
// {
//     os_specific.set_terminal_state(new_state);
// }

save_terminal_state :: () -> void
{
    os_specific.save_original_terminal_state();
}

reset_terminal_state :: () -> void
{
    os_specific.reset_terminal_state();
}

set_terminal_color :: (new_color: Terminal_Color, bold: bool)
{
    os_specific.set_terminal_color(new_color, bold);
}

get_page_size :: () -> s64
{
    return os_specific.get_page_size();
}

alloc_aligned :: (alignment: s64, size: s64) -> *void
{
    return os_specific.alloc_aligned(alignment, size);
}

free_aligned :: (ptr: *void)
{
    os_specific.free_aligned(ptr);
}

create_thread :: (thread_routine: Thread_Routine, user_data: *void) -> Thread
{
    static_if (ZODIAC_RUNNING_BYTECODE)
    {
        return __create_thread__(thread_routine, user_data);
    }
    else
    {
        start_info : Thread_Start_Info;
        start_info.thread.user_data = user_data;
        atomic_cond_init(*start_info.signal_started);
        atomic_cond_init(*start_info.signal_thread_copied);
        start_info.thread_routine = thread_routine;
        thread_ptr := *start_info.thread;

        success := os_specific._create_thread(*start_info);
        assert(success);

        atomic_cond_signal(*start_info.signal_started);
        atomic_cond_wait(*start_info.signal_thread_copied, null);

        result := start_info.thread;
        return result;
    }
}

create_thread :: (thread_routine: Thread_Routine, user_data: $T) -> Thread
{
    return create_thread(thread_routine, (:*void)user_data);
}

join_thread :: (thread: Thread) -> *void
{
    static_if (ZODIAC_RUNNING_BYTECODE)
    {
        return __join_thread__(thread);
    }
    else
    {
//         ret_val : *void = null;
//         success := pthread.pthread_join((:pthread.pthread_t)thread_handle, *ret_val);
//         assert(success == 0);

        return os_specific._join_thread(thread);
    }
}
